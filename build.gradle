plugins {
    id 'codenarc'
    id 'com.gradle.plugin-publish' version '0.9.9'
	id 'groovy'
	id 'java-gradle-plugin'
    id 'maven-publish'
}

version = '1.0.1-SNAPSHOT'
group = 'ru.kinca.gradle'

repositories {
	mavenCentral()
}

apply from: 'testing.gradle'

codenarcMain {
    configFile = file('config/codenarc/main.groovy')
}

String pluginId = 'ru.kinca.google-drive-uploader'
gradlePlugin {
    plugins {
        googleDriveUploader {
            id = pluginId
            implementationClass = 'ru.kinca.gradle.googledrive.GoogleDriveUploaderPlugin'
        }
    }
}
pluginBundle {
    website = 'https://github.com/valnaumov/gradle-google-drive-plugin'
    vcsUrl = 'git@github.com:valnaumov/gradle-google-drive-plugin.git'
    plugins {
        googleDriveUploader {
            id = pluginId
            displayName = 'Uploads files to Google Drive'
            description = 'A plugin that lets you upload/backup files to Google Drive'
            tags = ['google', 'drive', 'googleDrive', 'backup']
        }
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}
compileGroovy {
    groovyOptions.configurationScript = file('config/compiler-customizer.groovy')
}

wrapper {
    gradleVersion = '4.9'
}

configurations.all {
    exclude group: 'com.google.guava', module:'guava-jdk5'
}
dependencies {
    def spockDependency = dependencies.create('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }

	compile localGroovy()
	compile gradleApi()
    compile 'com.google.apis:google-api-services-drive:v3-rev127-1.24.1'
    compile 'com.google.api-client:google-api-client:1.25.0'
    compile 'com.google.oauth-client:google-oauth-client-jetty:1.25.0'
    testCompile spockDependency
    testRuntime 'cglib:cglib-nodep:3.2.5'
    testRuntime 'org.objenesis:objenesis:1.3'
    integrationTestCompile spockDependency
    functionalTestCompile spockDependency
    functionalTestCompile gradleTestKit()
    functionalTestCompile 'commons-io:commons-io:2.6'
}
